import { split, text_contains } from "std/text"
import { file_write, file_append, dir_exists, file_exists, dir_create } from "std/fs"

let path = "/tmp/amber-sc-tests"

if (not dir_exists(path)) {
    dir_create(path)
}

let report = "{path}/report.txt"
trust file_write(report, "Report for Shellcheck")
let output = ""

let stdtests = trust $ /usr/bin/ls "src/tests/stdlib/" $
let stdlib = split(stdtests, "\n")

for v in stdlib {
    if not text_contains(v, ".txt") and file_exists("src/tests/stdlib/{v}") {
        echo "Generating Bash script for test {v}"
        trust $ ./target/debug/amber "src/tests/stdlib/{v}" "{path}/{v}.sh" $
        output = trust $ shellcheck "{path}/{v}.sh" $

        if (status != 0) {
            echo "Shellcheck found something!"
            trust file_append(report, output)
        }
    }
}